import numpy as np

configfile: "config.yaml"

compression_modes = config['compression_modes']
block_sizes = config['block_sizes']
range_sizes = list(map(str, map(int, np.array([2, 4, 6, 10, 20]) * 100000)))[:1]
from_max_idx = 800000
msids = config["species"]


wildcard_constraints:
    compress="(" + ")|(".join(compression_modes) + ")",
    range="\d0{4,9}",
    msid="(" + ")|(".join(msids) + ")"

rule all:
    input:
        expand("{msid}.t.ms.csv", msid=msids)



rule collect:
    threads: 1
    input:
        map(lambda s: "{msid}." + s, expand("t.ms.{b}.{r}.{c}.csv",
                                            r=range_sizes,
                                            c=compression_modes,
                                            b=block_sizes))
    output:
        "{msid}.t.ms.csv"
    run:
        with open(str(output), "w") as fd:
            fd.write("compression,block_size,range_size,nqueries,method,time_ms\n")
            for i in input:
                print(i, " --> ", output)
                with open(str(i), "r") as infd:
                    for line in infd:
                        fd.write(line)



rule ridx_profile:
    wildcard_constraints:
        block_size="\d{2,4}"
    threads: 1
    input:
        i="{msid}.t.ms.{compress}",
        ps="{msid}.t.ms.none.{block_size}.ridx"
    output:
        temp("{msid}.t.ms.{block_size}.{range}.{compress}.csv")
    params:
        fmi=from_max_idx
    shell:
        ("{config[rq_profile_exe]} -from_max_idx {params.fmi} "
         "-ms_path {input.i} "
         "-compression {wildcards.compress} "
         "-ridx_path {input.ps} "
         "-range_size {wildcards.range} "
         "-block_size {wildcards.block_size} "
         "-niter 100000 >{output}")

rule noidix_profile:
    wildcard_constraints:
        block_size="-?[0,1]"
    threads: 1
    input:
        i="{msid}.t.ms.{compress}"
    output:
        temp("{msid}.t.ms.{block_size}.{range}.{compress}.csv")
    params:
        fmi=from_max_idx,
        niter=lambda ww: 100 if int(ww.block_size) < 0 else 100
    shell:
        ("{config[rq_profile_exe]} -from_max_idx {params.fmi} "
         "-ms_path {input.i} "
         "-compression {wildcards.compress} "
         "-range_size {wildcards.range} "
         "-block_size {wildcards.block_size} "
         "-niter {params.niter} >{output}")


rule partial_sums:
    threads: 1
    input:
        "{msid}.t.ms.none"
    benchmark:
        "{msid}.ridx_time.{block_size}.tsv"
    output:
        "{msid}.t.ms.none.{block_size}.ridx"
    shell:
        ("{config[dump_ridx_exe]} "
         "-ms_path {input} "
         "-block_size {wildcards.block_size}")


rule prepare_ms:
    input:
        "{msid}.t.ms"
    output:
        "{msid}.t.ms.{compress}",
        "{msid}.t.ms.{compress}.hist"
    wildcard_constraints:
        compress="(" + ")|(".join(compression_modes) + ")"
    threads: 1
    shell:
        ("if [ '{wildcards.compress}' == 'none' ] ; "
         "then cp -v {input} {output}; "
         "else {config[compress_exe]} -ms_path {input} -compression {wildcards.compress} > {output}.hist; "
         "fi")

