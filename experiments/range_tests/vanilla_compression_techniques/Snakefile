import numpy as np
import pandas as pd

configfile: "config.yaml"

compression_modes = config['compression_modes']
msids = config["species"]


wildcard_constraints:
    compress="(" + ")|(".join(compression_modes) + ")",
    msid="(" + ")|(".join(msids) + ")"


rule all:
    input:
        "ms_sizes.csv", "rmq_size.x"

rule ms_sizes:
    input:
        expand("{msid}.ms.{c}", msid=msids, c=compression_modes)
    output:
        "ms_sizes.csv"
    run:
        from pathlib import Path
        (pd.DataFrame([(Path(p).name, Path(p).stat().st_size)
                       for p in input], columns=["name", "size"])
         .to_csv(str(output), index=False))



rule compress_ms:
    input:
        "{msid}.ms"
    output:
        ms=protected("{msid}.ms.{compress}"),
        hist= "{msid}.ms.{compress}.hist"
    wildcard_constraints:
        compress="(" + ")|(".join(compression_modes) + ")"
    threads: 1
    shell:
        ("if [ '{wildcards.compress}' == 'none' ] ; "
         "then cp -v {input} {output.ms} && touch {output.hist}; "
         "else {config[compress_exe]} -ms_path {input} -compression {wildcards.compress} > {output.hist}; "
         "fi")

rule rmq_size_exe:
    input:
        m="makefile.rmq_size_exe",
        s="rmq_size.cpp"
    output:
        "rmq_size.x"
    shell:
        "make -f {input.m} clean && make -f {input.m} {output}"
